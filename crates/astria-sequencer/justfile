default:
  @just --list

set dotenv-load
set fallback

default_env := 'local'
copy-env type=default_env:
  cp {{ type }}.env.example .env

run:
  cargo run


run-cometbft:
  cometbft init
  ../../target/debug/astria-sequencer-utils --genesis-app-state-file=test-genesis-app-state.json --destination-genesis-file=$HOME/.cometbft/config/genesis.json
  sed -i'.bak' 's/timeout_commit = "1s"/timeout_commit = "15s"/g' ~/.cometbft/config/config.toml
  cometbft node

run-testnet: 
  cargo build
  ${OUT_DIR} ${NUM_VALIDATORS} ./scripts/testnet.sh

cometbft-logs-testnet NODE="node0":
  tail -f sequencer_testnet/{{ NODE }}/cometbft.log

sequencer-logs-testnet NODE="node0":
  tail -f sequencer_testnet/{{ NODE }}/sequencer.log

stop-testnet:
  pkill cometbft && pkill astria-sequencer

clean-testnet DIR="sequencer_testnet": stop-testnet
  rm -r {{ DIR }}
