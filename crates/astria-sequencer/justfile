default:
  @just --list

set dotenv-load
set fallback

default_env := 'local'
copy-env type=default_env:
  cp {{ type }}.env.example .env

run:
  cargo run

run-cometbft:
  #!/usr/bin/env bash
  set -e
  app_state_genesis="$(mktemp)"
  genesis="$(mktemp)"

  cometbft init

  # uncomment this line if you want to inspect `app_state_genesis`
  trap "rm -f ${app_state_genesis@Q}" EXIT
  cargo run -p astria-sequencer-utils -- \
    generate-genesis-state -o "${app_state_genesis}" --force
  cargo run -p astria-sequencer-utils -- \
    copy-genesis-state \
    --genesis-app-state-file="${app_state_genesis}" \
    --destination-genesis-file="$HOME/.cometbft/config/genesis.json" \
    --chain-id=astria
  jq ".consensus_params.abci.vote_extensions_enable_height = \"1\"" $HOME/.cometbft/config/genesis.json > "$genesis" && mv "$genesis" $HOME/.cometbft/config/genesis.json

  sed -i'.bak' 's/timeout_commit = "1s"/timeout_commit = "2s"/g' ~/.cometbft/config/config.toml
  cometbft node

[macos]
stop-testnet:
  pkill cometbft || true
  pkill astria-sequencer || true
[macos]
run-testnet: 
  cargo build
  ./scripts/testnet.sh

[linux]
stop-testnet:
  pkill -f [a]stria-sequencer || true
  pkill cometbft || true
[linux] 
run-testnet: 
  cargo build
  ./scripts/testnet.sh

cometbft-logs-testnet NODE="node0":
  tail -f sequencer_testnet/{{ NODE }}/cometbft.log

sequencer-logs-testnet NODE="node0":
  tail -f sequencer_testnet/{{ NODE }}/sequencer.log

clean-testnet DIR="sequencer_testnet": stop-testnet
  rm -r {{ DIR }} || true
  rm -r /tmp/astria_db_* || true
