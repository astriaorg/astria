default:
  @just --list

set dotenv-load
set fallback

default_env := 'local'
copy-env type=default_env:
  cp {{ type }}.env.example .env

run:
  cargo run

run-cometbft:
  cometbft init
  ../../target/debug/astria-sequencer-utils --genesis-app-state-file=test-genesis-app-state.json --destination-genesis-file=$HOME/.cometbft/config/genesis.json --chain-id=astria
  sed -i'.bak' 's/timeout_commit = "1s"/timeout_commit = "2s"/g' ~/.cometbft/config/config.toml
  cometbft node

[macos]
stop-testnet:
  pkill cometbft || true
  pkill astria-sequencer || true
[macos]
run-testnet: 
  cargo build
  ./scripts/testnet.sh

[linux]
stop-testnet:
  pkill -f [a]stria-sequencer || true
  pkill cometbft || true
[linux] 
run-testnet: 
  cargo build
  ./scripts/testnet.sh

cometbft-logs-testnet NODE="node0":
  tail -f sequencer_testnet/{{ NODE }}/cometbft.log

sequencer-logs-testnet NODE="node0":
  tail -f sequencer_testnet/{{ NODE }}/sequencer.log

clean-testnet DIR="sequencer_testnet": stop-testnet
  rm -r {{ DIR }} || true
  rm -r /tmp/astria_db_* || true
